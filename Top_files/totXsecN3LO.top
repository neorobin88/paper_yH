set device postscript
set size ORIENTATION 3
set window x  1.2 to 9.5
set window y  3.5 to 9.5

 SET FONT DUPLEX
 SET LIMITS X 20 140
 SET LIMITS y 35 52
 SET SCALE X LINEAR
 SET SCALE Y LINEAR 

set intensity 4

plot axes


set ticks size 0.05
set title size 2.0

title left "S2tot3  (pb)"
case       "GX   X      "

title bottom "M[M0R1=M0F1]   (GeV)"
case         "G GX X GX X        "

10000 10000
plot


data set name="l1"
set order x y
100 49.5

data set name="l2"
set order x y
100 48

data set name="l3"
set order x y dy ndy
28 39.5 0.5 0.5

data set name="l4"
set order x y dy ndy
28 36.5 0.5 0.5

data set name="l5"
set order x y dy ndy
28 41.0 0.5 0.5

data set name="l6"
set order x y dy ndy
28 38.0 0.5 0.5

title data 34 50.0 "2s=13 TeV"
case                "M       "


(n  central      DY   NDY 

data set name="totN3LO"
( Exact N^3LO values from ihixs 2
( Scale	Xsection
set order x y
31.25	45.0601
62.5	44.96739
125 	43.49829

								
data set name="totNNLO"
( Exact NNLO values from ihixs 2
( Scale	Xsection
set order x y
28.25	47.3284
59.5	43.4692
122 	39.6367

		
data set name="N3LOqT"
( N^3LO from qT subtraction at qTcut=1GeV Using CN3=-943
( Scale	Xsection
set order x y DY NDY
35.25	44.97  0.38   0.38
66.5	44.97  0.21   0.21
129 	43.51  0.12   0.12


data set name="N3LOqTCN30"
( N^3LO from qT subtraction at qTcut=1GeV Using CN3=0
( Scale	Xsection
set order x y DY NDY
39.25	46.44   0.38   0.38
70.5	45.86 	0.21   0.21
133 	44.075 	0.12   0.12

data set name="N3LOqTCN302"
( N^3LO from qT subtraction at qTcut=1GeV Using CN3=0
( Scale	Xsection
set order x y DY NDY
39.25	46.44   0.38   0.38
70.5	45.86 	0.21   0.21
133 	44.075 	0.12   0.12

data set name="N3LOqT2"
( N^3LO from qT subtraction at qTcut=2GeV Using CN3=-943
( Scale	Xsection
set order x y DY NDY
39.25	44.9465     0.31  0.31
70.5	44.98   	0.17  0.17
133 	43.5118 	0.1   0.1

data set name="N3LOqT3"
( N^3LO from qT subtraction at qTcut=3GeV Using CN3=-943
( Scale	Xsection
set order x y DY NDY
43.25	44.9222     0.28  0.28
74.5	45.0096 	0.15  0.15
137 	43.5259  	0.09  0.09


set color
set symbol 9o size 2
set fill entry full size 0.01 solid width 5
plot fill select="l1"
title data 107 49.5 "N233LO(M)"
case                " X X   G "
plot fill select="totN3LO"

set color green
set intensity 4
set symbol 9o size 2
set fill entry full size 0.01 solid width 5
plot fill select="l2"
set color
title data 107 48  "NNLO(M)"
case               "     G "
set color green
plot fill select="totNNLO"





set color cyan
set intensity 2

set symbol 30 size 1.5
set fill entry full size 0.01 solid width 5

plot fill select="l3"
set color
set intensity 4
title data 32 39.5 "N233LO(M) [q0T12(cut)3=1GeV;C0N31(q0T12(cut)3=1GeV )]"
case             " X X   G    X XX     X       X  X  X XX     X        "
set color cyan
set intensity 4
plot fill select="N3LOqT"



set intensity 4
set color red
set symbol 9o size 2
set fill entry full size 0.01 solid width 5
plot fill select="l5"
set color
title data 32 41 "N233LO(M) [q0T12(cut)3=2GeV;C0N31(q0T12(cut)3=1GeV )]"
case               " X X   G    X XX     X       X  X  X XX     X        "
set color red
plot fill select="N3LOqT2"






set color blue
set symbol 20 size 1.9
set fill entry full size 0.01 solid width 5

plot fill select="l6"
set color
title data 32 38.0 "N233LO(M) [q0T12(cut)3=3GeV;C0N31(q0T12(cut)3=1GeV )]"
case               " X X   G    X XX     X       X  X  X XX     X        "
set color blue
plot fill select="N3LOqT3"


set color magenta
set symbol 00 size 2
set fill entry full size 0.01 solid width 5
plot fill select="l4"
set color
title data 32 36.5 "N233LO(M) [q0T12(cut)3=1GeV; C0N31=0]"
case               " X X   G    X XX     X        X  X   "
set color magenta
plot fill select="N3LOqTCN30"

